Value Filldown sysname (\S+)
Value Filldown model (\S+)
Value source_ip (\S+)
Value l3vpn (\S+)
Value service_id (\d+)
Value service_name ([^"]+)
Value customer_id (\S+)
Value interface (\S+)
Value spoke_spd_id (\S+)
Value spoke_svc_id (\S+)
Value sap (\S+)
Value ingress_qos_id (\S+)
Value egress_qos_id (\S+)
Value sdp_id (\S+)
Value far_end_system_ip (\S+)
Value port (\S+)
Value queue_policy (\S+)
Value lag_id (\S+)
Value port_lag (\S+)
Value card (\S+)
Value mda (\d+)
Value fabric_network (\d+)
Value fabric_access (\d+)

Start
  ^.+ -oCheckHostIP -> Source
  ^.+ show chassis -> Chassis
  ^\s{4}port ${port} -> Ports
  ^\s{4}card ${card} -> Card
  ^\s{4}service -> Service
  ^Lag-id\s{14}:\s+${lag_id} -> Lag

Source
  ^.+@${source_ip}'s\s+password: -> Start

Chassis
  ^\s+Name\s+:\s${sysname}
  ^\s+Type\s+:\s${model} -> Start

Service
  ^\s{8}sdp ${sdp_id} mpls create -> Sdp
  ^\s{8}${l3vpn} ${service_id} name "${service_name}" customer ${customer_id} create -> Vpn
  ^\s{4}exit -> Start

Sdp
  ^\s{12}far-end ${far_end_system_ip} -> Record
  ^\s{8}exit -> Service

Vpn
  # ^\s{12}spoke-sdp ${spoke_spd_id}:${spoke_svc_id} -> Record
  ^\s{12}sap ${sap} create -> Sap
  ^\s{12}interface "${interface}" create -> Interface
  ^\s{8}exit -> Service

Interface
  ^\s{16}spoke-sdp ${spoke_spd_id}:${spoke_svc_id} -> Record
  ^\s{12}exit -> Vpn

Sap
  ^\s{16}ingress -> Ingress
  ^\s{16}egress -> Egress
  ^\s{12}exit -> Record Vpn

Ingress
  ^\s{20}qos ${ingress_qos_id}
  ^\s{16}exit -> Sap

Egress
  ^\s{20}qos ${egress_qos_id}
  ^\s{16}exit -> Sap

Ports
  ^\s{8}ethernet -> PortEthernet
  ^\s{4}exit -> Record Start

PortEthernet
  ^\s{12}network -> PortEthernetNetwork
  ^\s{8}exit -> Ports

PortEthernetNetwork
  ^\s{16}queue-policy "${queue_policy}"
  ^\s{12}exit -> PortEthernet

Lag
  ^${port_lag}.{36}yes -> Record Start

Card
  ^\s{8}mda ${mda} -> Mda
  ^\s{4}exit -> Start

Mda
  ^\s{12}network -> MdaNetwork
  ^\s{12}access -> MdaAccess
  ^\s{8}exit -> Record Card

MdaNetwork
  ^\s{20}fabric-policy ${fabric_network}
  ^\s{12}exit -> Mda

MdaAccess
  ^\s{20}fabric-policy ${fabric_access}
  ^\s{12}exit -> Mda
  
EOF
